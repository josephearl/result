import com.github.spotbugs.snom.SpotBugsTask

plugins {
  id 'java-library'
  id 'maven-publish'
  id 'io.freefair.lombok' version '8.6'
  id 'com.diffplug.spotless' version '6.25.0'
  id 'com.github.spotbugs' version '6.0.20'
}

group 'io.github.josephearl.result'
version '1.0.0-SNAPSHOT'

repositories {
  mavenCentral()
}

dependencies {
  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.3'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.3'
  testImplementation 'org.mockito:mockito-junit-jupiter:5.12.0'
  testImplementation 'org.assertj:assertj-core:3.26.3'
  testImplementation 'nl.jqno.equalsverifier:equalsverifier:3.16.1'
}

java {
  sourceCompatibility = JavaVersion.VERSION_17
  targetCompatibility = JavaVersion.VERSION_17
  withJavadocJar()
  withSourcesJar()
}

compileJava {
  options.encoding = 'UTF-8'
  options.compilerArgs << '-parameters'
}

compileTestJava {
  sourceCompatibility = JavaVersion.VERSION_21
  targetCompatibility = JavaVersion.VERSION_21
  options.encoding = 'UTF-8'
}

test {
  useJUnitPlatform()
}

spotless {
  java {
    importOrder()
    removeUnusedImports()
    googleJavaFormat()
    endWithNewline()
  }
}

spotbugs {
  excludeFilter = file('spotbugs-exclude.xml')
}

tasks.withType(SpotBugsTask).configureEach {
  reports {
    html.required = true
  }
}

tasks.withType(Javadoc).configureEach {
  options {
    addBooleanOption('Xdoclint:accessibility,html,reference,syntax', true)
    addBooleanOption('Xwerror', true)
  }
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
    }
  }
  repositories {
    maven {
      url = 'https://maven.pkg.github.com/josephearl/result'
      credentials {
        username = System.getenv('GITHUB_ACTOR')
        password = System.getenv('GITHUB_TOKEN')
      }
    }
  }
}
