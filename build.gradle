import com.github.spotbugs.snom.SpotBugsTask

plugins {
  id 'java-library'
  id 'maven-publish'
  id 'signing'
  id 'io.freefair.lombok' version '8.6'
  id 'com.diffplug.spotless' version '6.25.0'
  id 'com.github.spotbugs' version '6.0.8'
}

group 'io.github.josephearl'
version '1.0.0-SNAPSHOT'

repositories {
  mavenCentral()
}

dependencies {
  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.2'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.2'
  testImplementation 'org.mockito:mockito-junit-jupiter:5.11.0'
  testImplementation 'org.assertj:assertj-core:3.25.3'
  testImplementation 'nl.jqno.equalsverifier:equalsverifier:3.15.8'
}

java {
  sourceCompatibility = JavaVersion.VERSION_17
  targetCompatibility = JavaVersion.VERSION_17
  withJavadocJar()
  withSourcesJar()
}

compileJava {
  options.encoding = 'UTF-8'
  options.compilerArgs << '-parameters'
}

compileTestJava {
  sourceCompatibility = JavaVersion.VERSION_21
  targetCompatibility = JavaVersion.VERSION_21
  options.encoding = 'UTF-8'
}

test {
  useJUnitPlatform()
}

spotless {
  java {
    importOrder()
    removeUnusedImports()
    googleJavaFormat()
    endWithNewline()
  }
}

spotbugs {
  excludeFilter = file('spotbugs-exclude.xml')
}

tasks.withType(SpotBugsTask).configureEach {
  reports {
    html.required = true
  }
}

tasks.withType(Javadoc).configureEach {
  options {
    addBooleanOption('Xdoclint:accessibility,html,reference,syntax', true)
    addBooleanOption('Xwerror', true)
  }
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java

      pom {
        name = 'Result'
        description = 'Handle errors gracefully in a type-safe, functional manner similar to Kotlin\'s Result type or Scala\'s Try type using Java sealed classes'
        url = 'https://github.com/josephearl/result'
        licenses {
          license {
            name = 'The Apache License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }
        developers {
          developer {
            name = 'Joseph Earl'
          }
        }
        scm {
          url = 'https://github.com/josephearl/result'
        }
      }
    }
  }

  repositories {
    maven {
      name = "GitHubPackages"
      url = 'https://maven.pkg.github.com/josephearl/result'
      credentials {
        username = System.getenv('GITHUB_ACTOR')
        password = System.getenv('GITHUB_TOKEN')
      }
    }
    maven {
      name = "MavenCentral"
      url = "https://central.sonatype.com/"
      credentials {
        username = System.getenv("MAVEN_USERNAME")
        password = System.getenv("MAVEN_PASSWORD")
      }
    }
  }
}

signing {
  sign publishing.publications.mavenJava
}
