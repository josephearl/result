import com.github.spotbugs.snom.SpotBugsTask

plugins {
  id 'java-library'
  id 'maven-publish'
  id 'io.freefair.lombok' version '6.5.1'
  id 'com.diffplug.spotless' version '6.12.0'
  id 'com.github.spotbugs' version '5.0.13'
}

group 'com.waracle.result'
version '1.0-SNAPSHOT'

repositories {
  mavenCentral()
}

dependencies {
  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
  testImplementation 'org.mockito:mockito-junit-jupiter:4.9.0'
  testImplementation 'org.assertj:assertj-core:3.23.1'
  testImplementation 'nl.jqno.equalsverifier:equalsverifier:3.11.1'
}

java {
  sourceCompatibility = JavaVersion.VERSION_17
  targetCompatibility = JavaVersion.VERSION_17
  withJavadocJar()
  withSourcesJar()
}

compileJava {
  options.encoding = 'UTF-8'
  options.compilerArgs << '-parameters'
}

compileTestJava {
  sourceCompatibility = JavaVersion.VERSION_19
  targetCompatibility = JavaVersion.VERSION_19
  options.encoding = 'UTF-8'
  options.compilerArgs << '--enable-preview'
}

test {
  useJUnitPlatform()
  jvmArgs '--enable-preview'
}

spotless {
  java {
    // Google Java format cannot handle record patterns yet
    // https://github.com/google/google-java-format/issues/865
    targetExclude('src/test/java/com/waracle/result/ResultExample.java')
    importOrder()
    removeUnusedImports()
    googleJavaFormat()
    endWithNewline()
  }
}

spotbugs {
  excludeFilter = file('spotbugs-exclude.xml')
}

tasks.withType(SpotBugsTask) {
  reports {
    html {
      enabled = true
    }
  }
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
    }
  }
  repositories {
    maven {
      name = 'GitHubPackages'
      url = 'https://maven.pkg.github.com/josephearl/result'
      credentials {
        username = System.getenv('GITHUB_ACTOR')
        password = System.getenv('GITHUB_TOKEN')
      }
    }
  }
}
